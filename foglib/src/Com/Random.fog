namespace Random {
using/interface Lang;
using/interface Hash;
using/interface Native;




class Generator {
	$MakeMoveable();
	
	uint64 state[4];
	
	uint64 s_rotl(const uint64 x, int k) {
		return (x << k) | (x >> (64 - k)); // GCC/CLANG/MSC happily optimize this
	}
	
	uint64 sNext(uint64 *s) {
		const uint64 result_starstar = s_rotl(s[1] * 5, 7) * 9;
		const uint64 t = s[1] << 17;
	
		s[2] ^= s[0];
		s[3] ^= s[1];
		s[1] ^= s[2];
		s[0] ^= s[3];
	
		s[2] ^= t;
	
		s[3] = s_rotl(s[3], 45);
	
		return result_starstar;
	}
	
	void sSeed(uint64 *s) {
		for(int pass = 0; pass < 4; pass++) {
			for(int i = 0; i < 4; i++) {
				CombineHash h;
				h.Put64(NativeCurrentTime());
				h.Put64(NativeNanoSeconds());
				h.Put64(NativeMilliSeconds());
				s[i] ^= h;
			}
		}
	}
	
public:
	Generator() {Seed();}
	float  Randomf() {return (sNext(state) >> 11) * (1.f / (UINT64_C(1) << 53));}
	double Randomd() {return (sNext(state) >> 11) * (1.  / (UINT64_C(1) << 53));}
	uint64 Random(uint64 max) {return Get() % max;}
	uint64 Get() {return sNext(state);}
	operator uint64 () {return Get();}
	void Seed() {sSeed(state);}
	void Seed(dword seed) {
		for(int i = 0; i < 4; i++)
			state[i] = 12345678 + seed + i; // xoshiro does not work well if all is zero
	}
	
	static Generator& Local() {static thread_local Generator r; return r;}
};




dword Int() {return Generator::Local().Random(UINT32_MAX);}
uint64 Int64() {return Generator::Local().Get();}
dword Range(dword n) {return Generator::Local().Random(n);}
uint64 Range64(uint64 n) {return Generator::Local().Random(n);}
float  Float()  {return Generator::Local().Randomf();}
double Double() {return Generator::Local().Randomd();}
void Seed() {return Generator::Local().Seed();}


}


